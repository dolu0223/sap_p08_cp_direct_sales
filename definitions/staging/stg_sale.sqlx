config {
  type: "view",
  schema: "staging",
  description: "View for Net Sales Quantity, Total Order Quantity, Return Quantity"
}

SELECT DISTINCT
  SLS.ORDER_NUMBER,
  SLS.MATERIAL_NUMBER,
  SLS.DOCUMENT_TYPE_CODE,
  SLS.SALES_GROUP_CODE AS POSITION_LEVEL_1_CODE,
  MIN(SLS.DOCUMENT_DATE) AS FIRST_ORDER_DATE,
  SUM(SLS.BASE_UNIT_CUMMULATIVE_CONFIRMED_QUANTITY) AS NET_SALES_QUANTITY_LOCAL,
  SUM(
    CASE WHEN SLS.SALES_UNIT_OF_MEASURE_CODE <> CONV.BASE_UNIT_OF_MEASURE_CODE
      THEN CASE WHEN SLS.ORDER_ITEM_QUANTITY * CONV.TO_BASE_UNIT_OF_MEASURE_CONVERSION_FACTOR < 0
          THEN 0
          ELSE SLS.ORDER_ITEM_QUANTITY * CONV.TO_BASE_UNIT_OF_MEASURE_CONVERSION_FACTOR
        END
      ELSE
        CASE WHEN SLS.ORDER_ITEM_QUANTITY < 0
          THEN 0
          ELSE SLS.ORDER_ITEM_QUANTITY
        END
    END
  ) AS TOTAL_ORDER_QUANTITY_LOCAL,
  SUM(
    CASE WHEN SLS.SALES_UNIT_OF_MEASURE_CODE <> CONV.BASE_UNIT_OF_MEASURE_CODE
      THEN CASE WHEN SLS.ORDER_ITEM_QUANTITY * CONV.TO_BASE_UNIT_OF_MEASURE_CONVERSION_FACTOR < 0
          THEN SLS.ORDER_ITEM_QUANTITY * CONV.TO_BASE_UNIT_OF_MEASURE_CONVERSION_FACTOR * -1
          ELSE 0
        END
      ELSE
        CASE WHEN SLS.ORDER_ITEM_QUANTITY < 0
          THEN SLS.ORDER_ITEM_QUANTITY * -1
          ELSE 0
        END
    END
  ) AS RETURN_QUANTITY_LOCAL
FROM
  ${ref('stg_sls_ord_dtl_emea')} SLS
LEFT JOIN
  ${ref('stg_conversion')} CONV
  ON SLS.MATERIAL_NUMBER = CONV.MATERIAL_NUMBER
  AND SLS.SALES_UNIT_OF_MEASURE_CODE = CONV.ALTERNATE_UNIT_OF_MEASURE_CODE
WHERE
  SLS.SALES_DISTRICT_CODE = 'AG-GB'
  AND SLS.SALES_ORGANIZATION_CODE = 'GB01'
  AND SLS.MATERIAL_GROUP_CODE NOT IN ('VRP01')
  AND SLS.DOCUMENT_TYPE_CODE IN ('ZOR1', 'ZU01', 'ZFD1', 'ZRE1', 'ZRV1', 'ZCQ1', 'ZCV1', 'ZDQ1', 'ZDV1', 'ZKB1', 'ZKE1', 'ZKR1', 'ZKA1', 'ZCA1', 'ZOS1')
  AND SLS.REJECTION_REASON_CODE = ''
  AND SLS.DISTRIBUTION_CHANNEL_CODE NOT IN ('38', '50')
  AND SLS.DIVISION_CODE = '15'
GROUP BY
  SLS.ORDER_NUMBER,
  SLS.MATERIAL_NUMBER,
  SLS.DOCUMENT_TYPE_CODE,
  SLS.SALES_GROUP_CODE